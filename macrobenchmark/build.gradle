plugins {
    id 'com.android.test'
    id 'kotlin-android'
}

android {

    compileSdk 31

    defaultConfig {
        minSdk 23
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    targetProjectPath = ":app" // Note that your module name may be different

    // Enable the benchmark to run separately from the app process
    experimentalProperties["android.experimental.self-instrumenting"] = true
    buildTypes {
        // Declare a build type to match the target app's build type
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
        }
    }
}

androidComponents {
    beforeVariants(selector().all()) {
        // Enable only the benchmark buildType, since we only want to measure
        // release-like build performance (should match app buildType)
        enabled = buildType == 'benchmark'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'junit:junit:4.13.2'
    implementation 'androidx.test.ext:junit:1.1.3'
    implementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.1.0-alpha13'
    implementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}